extends VBoxContainer

const QuestLogEntryNeededItem = preload("uid://daig4m4h8yb0m")

@export var quest: Quest

var eventbus := Eventbus
var gdata := GData
@onready var teaser := $Teaser

## @type {QuestLogEntryNeededItem[]}
var need_item_labels := []


func _ready():
	quest.progress_updated.connect(_on_progress_updated)
	quest.quest_completed.connect(_on_quest_completed)
	eventbus.locale_changed.connect(refresh)
	refresh()


func refresh() -> void:
	for child in get_children():
		if child != teaser:
			child.queue_free()
	need_item_labels = []

	teaser.text = gdata.get_localized_label(quest.base_data.teaser)
	for need in quest.progress:
		var label = QuestLogEntryNeededItem.instantiate()
		label.text = "· {current_amount}/{required_amount} {item_name}".format(
			{
				"current_amount": need.current_amount,
				"required_amount": need.required_amount,
				"item_name": gdata.get_localized_item_label(need.id)
			}
		)
		add_child(label)
		need_item_labels.append(label)


func _on_progress_updated() -> void:
	if not need_item_labels:
		for need in quest.progress:
			var label = QuestLogEntryNeededItem.instantiate()
			add_child(label)
			need_item_labels.append(label)

	for i in len(quest.progress):
		var label = need_item_labels[i]
		var need = quest.progress[i]
		label.text = "· {current_amount}/{required_amount} {item_name}".format(
			{
				"current_amount": need.current_amount,
				"required_amount": need.required_amount,
				"item_name": gdata.get_localized_item_label(need.id)
			}
		)


func _on_quest_completed() -> void:
	for i in len(quest.progress):
		var label = need_item_labels[i]
		var need = quest.progress[i]
		label.text = "~~~~~~· {current_amount}/{required_amount} {item_name}".format(
			{
				"current_amount": need.current_amount,
				"required_amount": need.required_amount,
				"item_name": gdata.get_localized_item_label(need.id)
			}
		)
